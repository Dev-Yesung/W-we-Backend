buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:3.2.0"
        classpath "io.spring.gradle:dependency-management-plugin:1.1.4"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.wave'
    version = '0.0.1-SNAPSHOT'

    sourceCompatibility = '17'
    targetCompatibility = '17'
    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    dependencies {
        // Spring Web Dependencies
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        // Spring Data Dependencies
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-data-redis'

        // Spring Kafka Dependencies
        implementation 'org.springframework.kafka:spring-kafka'

        // QueryDsl Dependencies
        implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
        annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
        annotationProcessor "jakarta.annotation:jakarta.annotation-api"
        annotationProcessor "jakarta.persistence:jakarta.persistence-api"

        // Spring Security Dependencies
        implementation 'org.springframework.boot:spring-boot-starter-security'
        testImplementation 'org.springframework.security:spring-security-test'

        // Lombok Dependencies
        annotationProcessor 'org.projectlombok:lombok'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
    }

    def querydslSrcDir = 'src/main/generated'

    clean {
        delete file(querydslSrcDir)
    }

    tasks.withType(JavaCompile) {
        options.generatedSourceOutputDirectory = file(querydslSrcDir)
    }

    test {
        useJUnitPlatform()
    }
}

project(':wave-core') {
    bootJar { enabled = false }
    jar { enabled = true }

    dependencies {
        implementation 'org.apache.commons:commons-lang3:3.12.0'
        runtimeOnly 'com.h2database:h2'
        runtimeOnly 'com.mysql:mysql-connector-j'
    }
}

project(':wave-api') {
    bootJar { enabled = true }
    jar { enabled = false }

    dependencies {
        implementation project(':wave-core')
        implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
        implementation 'io.jsonwebtoken:jjwt-impl:0.12.3'
        implementation 'io.jsonwebtoken:jjwt-jackson:0.12.3'
    }
}

project(':wave-message') {
    bootJar { enabled = true }
    jar { enabled = false }

    dependencies {
        implementation project(':wave-core')
        implementation 'org.springframework.boot:spring-boot-starter-mail'
        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    }
}

project(':wave-media') {
    bootJar { enabled = true }
    jar { enabled = false }

    dependencies {
        implementation project(':wave-core')
    }
}
